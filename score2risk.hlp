{smcl}
{* 06Feb2008}{...}
{hline}
help for {hi:score2risk}{center: S Kaptoge, L Pennells, S Hageman May 2021}
{hline}

{title:Systematic COronary Risk Evaluation (SCORE2) CVD risk score}

{p 4 12 2}
{cmd:score2risk}
[
{cmd:, } {opt ccodes}
{opt riskreg(numlist integer max=1 >=1 <=4)} {opt replace} {opt percent}
{opt shortlab} {opt score2op} {opt score2dm}
]


{title:Description}

{p 4 4}
{cmd:score2risk} is a program that calculates 10-year risk of cardiovascular disease (CVD) according to the new
SCORE2 algorithm as described in the published methods paper (SCORE2 working group and European Society of Cardiology's 
Cardiovascular Risk Collaboration* Eur Heart J 2021(42)2439-2454).
{browse "https://pubmed.ncbi.nlm.nih.gov/34120177"} 

{p 4 4}
Briefly, it calculates predicted 10-year CVD risk based on a new risk model derived using data from the Emerging Risk Factors 
Collaboration (ERFC) and recalibrates the predicted risks to reflect the expected 10-year risk in contemporary populations in 
4 defined risk regions. The latter recalibration was achieved using age- and sex-specific CVD incidences estimated by rescaling 
country-specific CVD mortality rates from the World Health Organisation (WHO) mortality database by age-and ssex- specific 
multipliers calculated using registry and cohort data on 10.8 million individuals; combined with risk factor values from the 
Non-Communicable Diseases Risk Factor Collaboration (NCD-RisC). 

{p 4 4}
The new SCORE2 risk algorithm is sex-specific and estimates the risk of fatal or non-fatal CVD. It includes as predictors 
age, smoking status, systolic blood pressure, history of diabetes, total cholesterol, high-density lipoprotein cholesterol, 
and interactions of all predictor variables and age.


{space 4}{title:Definition of required variables}

{p 4 4}
{cmd:score2risk} assumes relevant Stata dataset is loaded in memory and that required predictor variables  
{ul:have been named and coded (including units of measurements) as follows}:{break}
{it:ccode}{space 4} - 3 letter country code identifier (ISO 3166-1 alpha-3){break}
{it:riskreg}{space 2} - Risk region (1 = Low risk, 2 = Moderate risk, 3 = High risk, 4 = Very high risk) {break}
{it:sex}{space 6} - Sex (1 = Male, 2 = Female){break}
{it:ages}{space 5} - Age (years){break}
{it:smallbin}{space 1} - Smoking status (0 = Other, 1 = Current){break}
{it:hxdiabbin}{space 0} - History of diabetes (0 = No, 1 = Yes){break}
{it:sbp}{space 6} - Systolic blood pressure (mm Hg){break}
{it:tchol}{space 4} - Total cholesterol (mmol/L){break}
{it:hdl}{space 6} - High-density lipoprotein cholesterol (mmol/L){break}
It is the user's responsibility to ensure that the above naming and coding scheme is adhered to.
The relevant 3 letter country code identifier can be looked up using the option {opt ccodes}.

{p 4 4}
{bf:NB} New variables generated by {cmd:score2risk} will be located between specially named marker variables
({it:_SCORE2_START} and {it:_SCORE2_END}) which are dropped without warning on each use of the program.

{p 4 4}
{opt score2risk} can also optionally calculate adapted versions of the SCORE2 algorithim, including: {break}

{p 4 4}
{opt score2op} option to calculate SCORE2-OP risk, a version recommended for older adults aged 70 years or older 
(Eur Heart J 2021(42)2455-2467). {browse "https://pubmed.ncbi.nlm.nih.gov/34120185"} 

{p 4 4}
{opt score2dm} option to calculate SCORE2-Diabetes risk, a version further adapted for use in people with diabetes 
(Eur Heart J 2023(April 04)Epub). {browse "https://pubmed.ncbi.nlm.nih.gov/TBC"}{break}
The {opt score2dm} option requires the following additional variables to be present:{break}
{it:agediab}{space 7} - Age at diabetes diagnosis (years){break}
{it:hba1c_ifcc}{space 4} - HbA1c (mmol/mol){break}
{it:lnegfr_ckdepi}{space 1} - Log eGFR CKD-EPI (ml/min/1.73m^2){break}


{title:options}

{p 4 8}{opt ccodes}
displays a lookup table of 3 letter country codes (ISO 3166-1 alpha-3) and numeric risk region codes, with countries grouped according to 4 defined risk regions.
If minimum required variables are not present, this option will simply produce an on screen list of country codes and numeric code for the relevant SCORE2 risk region and no further results. 
Its main purpose is to help the user more easily identify an exact risk region code that should be specified when requested, or alternatively 
added as a numeric variable named riskreg in the dataset before calculations.

{p 4 8}{opt riskreg(numlist integer max=1 >=1 <=4)}
option to fix risk region variable as provided, and overrides presence of country code in the dataset. The values correspond to: {break}
1 = Low risk region {break}
2 = Moderate risk region {break}
3 = High risk region {break}
4 = Very high risk region {break}

{p 4 8}{opt replace}
signals it is okay to replace values in existing risk region variable if {opt riskreg()} is specified.

{p 4 8}{opt percent}
convert risk estimates to percentages.

{p 4 8}{opt shortlab}
use short variable labels on risk variables.

{p 4 8}{opt score2op} 
option to calculate SCORE2-OP risk as well (see above). If this option is specified, the output will have 3 variables, 
generated: score2, score2op, and score2ov, where score2ov is the combined result of SCORE2 and SCORE2-OP risk
predictions (i.e. with SCORE2-OP risk predictions used for adults aged 70 years or older).

{p 4 8}{opt score2dm} 
option to calculate SCORE2-Diabetes risk as well (see above).


{title:Examples}

{bf:* Example 1}
* NB: unavailable variables are generated for illustrative purposes only
webuse nhanes2, clear
gen ccode = "USA"
clonevar ages = age
clonevar hxdiabbin = diabetes
generate smallbin = rbinomial(1, 0.20)
clonevar sbp = bpsystol
generate tchol = tcresult*0.02586
generate hdl = hdresult*0.02586
generate agediab = rnormal(58, 12) if hxdiabbin == 1
generate hba1c_ifcc = rnormal(58, 17) if hxdiabbin == 1
generate egfr_ckdepi = rnormal(74, 20) if hxdiabbin == 1
generate lnegfr_ckdepi = ln(egfr_ckdepi)
score2risk
score2risk, riskreg(1) replace
score2risk, riskreg(2) replace
score2risk, riskreg(2) replace score2op
score2risk, riskreg(2) replace score2dm

{title:Authors}

{p 4 4}
Dr Stephen Kaptoge {break}
Dr Lisa Pennells {break}
Cardiovascular Epidemiology Unit {break}
Dept of Public Health and Primary Care {break}
University of Cambridge {break}
Strangeways Research Laboratory {break}
Worts Causeway {break}
Cambridge CB1 8RN {break}
UK {break}

{p 4 4}
Steven Hageman {break}
Dept of Vascular Medicine {break}
University Medical Center Utrecht {break}
Utrecht University {break}
Utrecht {break}
The Netherlands {break}


{title:Remarks}

{p 4 4}
The {cmd:score2risk} program has mainly been developed to facilitate calculation and further evaluation 
of the revised SCORE2 CVD risk models. Details of the methods and results of previous evaluation 
are described in the paper below.

{p 4 4}
It is important to note that the because the new SCORE2 risk models have been recalibrated to 
reflect the expected 10-year risk in contemporary populations in 4 European risk regions, 
they may be not be well suited for comparison with historically collected data.


{title:References}

{p 4 4}
SCORE2 working group and European Society of Cardiology's Cardiovascular Risk Collaboration* 
SCORE2 risk prediction algorithms: new models to estimate 10-year risk of cardiovascular disease in Europe.
Eur Heart J 2021(42)2439-2454.{break}
{browse "https://pubmed.ncbi.nlm.nih.gov/34120177"}{break}
{browse "https://academic.oup.com/eurheartj/article/42/25/2439/6297709"}{break}

{p 4 4}
SCORE2-OP Eur Heart J 2021(42)2455-2467.{break}
{browse "https://pubmed.ncbi.nlm.nih.gov/34120185"}{break}
{browse "https://academic.oup.com/eurheartj/article/42/25/2455/6297711"}{break}

{p 4 4}
SCORE2-Diabetes Eur Heart J 2023(April 04)Epub.{break}
{browse "https://pubmed.ncbi.nlm.nih.gov/TBC"}{break}
{browse "https://academic.oup.com/eurheartj/article/TBC"}{break}

{title:Also see}

{p 4 13}
Manual:  {hi:[R] meta - Meta-analysis},{break}

{p 4 13}
Online:  help for
{bf:{help predaddc}},
{bf:{help predcalib}} (if installed).
{p_end}
